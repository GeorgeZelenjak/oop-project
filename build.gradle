allprojects {

    repositories {
        mavenCentral()
    }

    group = 'nl.tudelft.oopp'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'application'

    ext {
        set('snippetsDir', file("build/generated-snippets"))
    }

    sourceCompatibility = 13

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [//client-side
                        'nl/tudelft/oopp/livechat/controllers/AlertController.class',
                        'nl/tudelft/oopp/livechat/controllers/NavigationController.class',
                        'nl/tudelft/oopp/livechat/controllers/*.class',
                        'nl/tudelft/oopp/livechat/controllers/scenecontrollers/**',
                        'nl/tudelft/oopp/livechat/controllers/cellcontrollers/**',
                        'nl/tudelft/oopp/livechat/controllers/popupcontrollers/**',
                        'nl/tudelft/oopp/livechat/views/**',
                        'nl/tudelft/oopp/livechat/uielements/**',
                        //server-side
                        'nl/tudelft/oopp/livechat/*.class',
                        'nl/tudelft/oopp/livechat/config/**'
                ])
            }))
        }

        reports {
            xml.enabled true
            csv.enabled false
            html.destination file("${buildDir}/reports/jacoco/html")
            xml.destination file("${buildDir}/jacoco/jacoco.xml")
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                enabled = true
                element = 'CLASS'
                includes = ['nl.tudelft.*']
                excludes = ['nl.tudelft.oopp.livechat.views',
                            'nl.tudelft.oopp.livechat.uielements',
                            'nl.tudelft.oopp.livechat.MainApp',
                            'nl.tudelft.oopp.livechat.config',
                            'nl.tudelft.oopp.livechat.MainApplication']

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.4
                }
            }
        }
    }

    checkstyle {
        toolVersion "8.25"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')

        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
    }


    test {
        outputs.dir snippetsDir
        useJUnitPlatform()

        jacoco {
            enabled = true
            includes = ['nl.tudelft.*']
        }

    }
}
